apply plugin: 'war'
apply plugin: 'liberty'

group = 'io.openliberty.benchmark'
version = '1.0-SNAPSHOT'
description = "TechEmpower Framework Benchmarks for Open Liberty"

sourceCompatibility = 1.8
targetCompatibility = 1.8
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'net.wasdev.wlp.gradle.plugins:liberty-gradle-plugin:2.5'
    }
}

repositories {
    mavenCentral()
}

dependencies {
    providedCompile group: 'io.openliberty.features', name: 'jaxrs-2.1', version: '18.0.0.3'
    providedCompile group: 'io.openliberty.features', name: 'jsonp-1.1', version: '18.0.0.3'
    libertyRuntime group: 'io.openliberty', name: 'openliberty-runtime', version: '18.0.0.3'
}

ext {
    appName = project.name
    testServerHttpPort = 9080
    testServerHttpsPort = 9443
}

liberty {
    server {
        name = "${appName}Server"
        configFile = file("src/main/liberty/config/server.xml")
        bootstrapProperties = ['default.http.port': testServerHttpPort,
                               'default.https.port': testServerHttpsPort,
                               'app.context.root': "/"]
        packageLiberty {
            archive = "$buildDir/${appName}.jar"
            include = "minifiy,runnable"
        }
    }
}

war {
    archiveName = "${baseName}.${extension}"
}

clean.dependsOn 'libertyStop'
libertyPackage.dependsOn 'libertyStop'
